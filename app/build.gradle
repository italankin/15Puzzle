plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId 'com.italankin.fifteen'
        versionCode 44
        versionName "1.15.2"
        minSdkVersion 19
        targetSdkVersion 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file('keystore-debug/debug.keystore')
            storePassword 'android'
            keyAlias 'debug'
            keyPassword 'android'
        }
        release {
            Properties propsRelease = new Properties()
            File propsFileRelease = file('keystore-release/release.properties')
            if (propsFileRelease.exists()) {
                propsRelease.load(new FileInputStream(propsFileRelease))

                storeFile file('keystore-release/' + propsRelease['keystore'])
                storePassword propsRelease['keystore_password']
                keyAlias propsRelease['alias']
                keyPassword propsRelease['alias_password']
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
        }
        release {
            debuggable false
            signingConfig signingConfigs.release
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
}
